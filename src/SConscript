# -*- mode: python -*-

preset_cxxflags = {
    'release':     [ "-O3", "-s"  ],
    'profile':     [ "-O2", "-g3", "-pg" ],
    'debug':       [ "-O0", "-g3" ],
    'development': [ "-O2", "-g3",
                     "-ansi",
                     "-pedantic",
                     "-Wall",
                     "-Wextra",
                     "-Wnon-virtual-dtor",
                     # "-Weffc++",
                     "-Wconversion",
                     "-Werror",
                     "-Wshadow",
                     "-Wcast-qual",
                     "-Winit-self", # only works with >= -O1
                     "-Wno-unused-parameter",
                     
                     # "-Winline",
                     # "-Wfloat-equal",
                     # "-Wunreachable-code",
                     ]
    }

preset_linkflags = {
    'release': [],
    'profile': [ "-pg" ],
    'debug':   [],
    'development': []
    }

Import("compile_galapix_gtk",
       "compile_galapix_sdl",
       "compile_spacenav")

libgalapix_sources = [
    # 'job.cpp',
    # 'md5.cpp',
    'blob.cpp',
    'database.cpp',
    'database_thread.cpp',
    'exec.cpp',
    'file_database.cpp',
    'file_entry.cpp',
    'file_reader.cpp',
    'filesystem.cpp',
    'image.cpp',
    'job_handle.cpp',
    'job_manager.cpp',
    'job_worker_thread.cpp',
    'jobs/test_job.cpp',
    'jobs/tile_generation_job.cpp',
    'lisp/getters.cpp',
    'lisp/lexer.cpp',
    'lisp/lisp.cpp',
    'lisp/parser.cpp',
    'math.cpp',
    'math/rect.cpp',
    'math/size.cpp',
    'math/vector2f.cpp',
    'math/vector2i.cpp',
    'math/vector3f.cpp',
    'plugins/curl.cpp',
    'plugins/imagemagick.cpp',
    'plugins/jpeg.cpp',
    'plugins/jpeg_memory_dest.cpp',
    'plugins/jpeg_memory_src.cpp',
    'plugins/kra.cpp',
    'plugins/png.cpp',
    'plugins/pnm.cpp',
    'plugins/rar.cpp',
    'plugins/rsvg.cpp',
    'plugins/tar.cpp',
    'plugins/ufraw.cpp',
    'plugins/xcf.cpp',
    'plugins/zip.cpp',
    'selection.cpp',
    'sexpr_file_reader.cpp',
    'sexpr_file_writer.cpp',
    'software_surface.cpp',
    'sqlite/connection.cpp',
    'sqlite/error.cpp',
    'sqlite/reader.cpp',
    'sqlite/statement.cpp',
    'surface.cpp',
    'texture.cpp',
    'thread.cpp',
    'tile_database.cpp',
    'tools/grid_tool.cpp',
    'tools/move_tool.cpp',
    'tools/pan_tool.cpp',
    'tools/resize_tool.cpp',
    'tools/rotate_tool.cpp',
    'tools/view_rotate_tool.cpp',
    'tools/zoom_rect_tool.cpp',
    'tools/zoom_tool.cpp',
    'url.cpp',
    'viewer_state.cpp',
    'workspace.cpp',
    ]

galapix_sources = [
    'framebuffer.cpp',
    'galapix.cpp',
    'viewer.cpp',
    ]

spacenav_sources = ['space_navigator.cpp']

sdl_sources = ['sdl/sdl_framebuffer.cpp',
               'sdl/sdl_viewer.cpp']

gtk_sources =['gtk/gtk_viewer.cpp',
              'gtk/gtk_viewer_widget.cpp']

optional_sources = []
optional_defines = []
optional_libs    = []

if compile_spacenav:
    optional_sources += spacenav_sources
    optional_defines += [('HAVE_SPACE_NAVIGATOR', 1)]
    optional_libs    += ['spnav']

BuildDir('build', 'src')

# build the base library
libgalapix_env = Environment(CXXFLAGS=preset_cxxflags['development'],
                             CPPPATH=['.'],
                             CPPDEFINES = optional_defines,
                             LIBS = ['GL', 'GLEW', 'sqlite3', 'jpeg', 'boost_thread-mt'] + optional_libs)
libgalapix_env.ParseConfig('pkg-config libpng --libs --cflags | sed "s/-I/-isystem/g"')
libgalapix_env.ParseConfig('sdl-config --cflags --libs | sed "s/-I/-isystem/g"')
libgalapix_env.ParseConfig('Magick++-config --libs --cppflags | sed "s/-I/-isystem/g"')
libgalapix_env.ParseConfig('pkg-config --cflags --libs libcurl | sed "s/-I/-isystem/g"')
libgalapix = libgalapix_env.StaticLibrary('galapix.sdl', 
                                          libgalapix_sources + optional_sources)

if compile_galapix_sdl:
    sdl_env = Environment(CXXFLAGS=preset_cxxflags['development'],
                          CPPPATH=['.'],
                          CPPDEFINES = ['GALAPIX_SDL'] + optional_defines,
                          LIBS = [libgalapix, 'GL', 'GLEW', 'sqlite3', 'jpeg', 'boost_thread-mt'] + optional_libs,
                          OBJPREFIX="sdl.")
    sdl_env.ParseConfig('pkg-config libpng --libs --cflags')
    sdl_env.ParseConfig('sdl-config --cflags --libs')
    sdl_env.ParseConfig('Magick++-config --libs --cppflags')
    sdl_env.ParseConfig('pkg-config --cflags --libs libcurl')
    sdl_env.Program('galapix.sdl', 
                    galapix_sources + sdl_sources + optional_sources)

if compile_galapix_gtk:
    gtk_env = Environment(CXXFLAGS=preset_cxxflags['development'],
                          CPPPATH=['.'],
                          CPPDEFINES = ['GALAPIX_GTK'] + optional_defines,
                          LIBS = [libgalapix, 'GL', 'GLEW', 'sqlite3', 'jpeg', 'boost_thread-mt'] + optional_libs,
                          OBJPREFIX="gtk.")
    gtk_env.ParseConfig('pkg-config libpng --libs --cflags')
    gtk_env.ParseConfig('sdl-config --cflags --libs')
    gtk_env.ParseConfig('Magick++-config --libs --cppflags')
    gtk_env.ParseConfig('pkg-config --cflags --libs libcurl')
    gtk_env.ParseConfig('pkg-config --cflags --libs gtkmm-2.4 libglademm-2.4 gtkglextmm-1.2')
    gtk_env.Program('galapix.gtk', 
                    galapix_sources + gtk_sources + optional_sources)

# EOF #
